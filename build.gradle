apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'sonar'

group = 'com.github.jelmerk'
archivesBaseName = 'liferay-plugin'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = 1.5
targetCompatibility = 1.5

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
}

signing {
    sign configurations.archives
}

javadoc {
    options.links = ['http://gradle.org/docs/1.0/javadoc/', 'http://docs.oracle.com/javase/1.5.0/docs/api/']
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment ->
                signing.signPom(deployment)
            }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
        }
    }
}

configure(install.repositories.mavenInstaller) {
    pom.project {

        packaging = 'jar'

        description 'Liferay plugin for Gradle'
        url 'https://github.com/jelmerk/liferay-gradle-plugin'

        scm {
            connection 'scm:git:http://github.com/jelmerk/liferay-gradle-plugin.git'
            developerConnection 'scm:git:https://github.com/jelmerk/liferay-gradle-plugin.git'
            url 'https://github.com/jelmerk/liferay-gradle-plugin'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jelmerk'
                name 'Jelmer Kuperus'
                timezone 'Europe/Amsterdam'
            }
        }
    }
}

sonar {
//    profile = 'Sun checkstyle checks'

    server {
        url = 'http://localhost:9000'
    }
    database {
        url = 'jdbc:mysql://localhost:3306/sonar'
        driverClassName = 'com.mysql.jdbc.Driver'
        username = 'sonar'
        password = 'sonar'
    }

    project {
        dynamicAnalysis = "reuseReports"
        withProjectProperties { props ->
            props["sonar.core.codeCoveragePlugin"] = "jacoco"
            props["sonar.jacoco.reportPath"] = "${buildDir}/jacoco.exec"
        }
    }
}

configurations {
    jacoco
}

dependencies {
    jacoco group: 'org.jacoco', name: 'org.jacoco.agent', version: 'org.jacoco', classifier: 'runtime'
}

test {
    jvmArgs '-javaagent:${configurations.jacoco.asPath}=destfile=${buildDir}/jacoco.exec,includes=com.github.jelmerk.*'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0'
}